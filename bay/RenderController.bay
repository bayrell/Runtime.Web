/*!
 *  Bayrell Runtime Library
 *
 *  (c) Copyright 2016-2021 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Runtime.Web;

use Runtime.lib;
use Runtime.BaseObject;
use Runtime.Web.Events.ChangeEvent;
use Runtime.Web.Events.WebEvent;
use Runtime.Web.Component;
use Runtime.Web.Message;
use Runtime.Web.RenderCommand;
use Runtime.Web.RenderDriver;
use Runtime.Web.VirtualDom;


class RenderController extends BaseObject
{
	RenderDriver driver = null;
	string class_name = "";
	Collection model_path = null;
	Dict params = null;
	var content = null;
	var elem = null;
	string controller_name = "";
	
	/* Virtual DOM */
	VirtualDom vdom = new VirtualDom{ "kind": "root", "controller": this, "pos": 0 };
	
	/* Pools */
	Vector<VirtualDom> render_partial = new Vector();
	Vector<VirtualDom> render_vdom = new Vector();
	Map<Component> components = new Map();
	
	
	
	/**
	 * Add render virtual dom
	 */
	public void addRenderVirtualDom(VirtualDom vdom, bool append = true)
	{
		/* Find item */
		int pos = this.render_vdom.indexOf(vdom);
		if (pos != -1) return;
		
		/* Push value */
		if (append)
		{
			this.render_vdom.pushValue(vdom);
		}
		else
		{
			this.render_vdom.prependValue(vdom);
		}
	}
	
	
	
	/**
	 * Render init
	 */
	public void renderInit()
	{
		this.render_partial = new Vector();
		this.render_commands = new Vector();
	}
	
	
	
	/**
	 * Render
	 */
	public void render()
	{
		/* Call render */
		this.vdom.prepareRepaint();
		this.addRenderVirtualDom(this.vdom);
		this.vdom.path_id = [ this.controller_name ];
		this.vdom.instance = this.elem;
		this.vdom.name = this.controller_name;
		this.vdom.e("c", this.class_name, this.params.concat({ "@key": this.class_name, "@bind": ["", this.model_path] }));
		this.vdom.p();
		
		/* Call partial */
		this.renderPartial();
	}
	
	
	
	/**
	 * Render partial
	 */
	public void renderPartial()
	{
		while (this.render_partial.count() != 0)
		{
			Collection render_partial = this.render_partial.toCollection();
			this.render_partial = new Vector();
			
			for (int i=0; i<render_partial.count(); i++)
			{
				VirtualDom item = render_partial[i];
				if (item.kind == VirtualDom::KIND_COMPONENT or item.kind == VirtualDom::KIND_FUNCTION)
				{
					this.renderVirtualDom(item);
					item.p();
				}
			}
		}
	}
	
	
	
	/**
	 * Render component
	 */
	public void renderVirtualDom(VirtualDom vdom)
	{
		/* Render component */
		if (vdom.kind == VirtualDom::KIND_COMPONENT)
		{
			string path_str = vdom.path_id.join(":");
			
			/* Get component by path */
			Component item = this.components.get(path_str, null)
			if (item != null and item.getClassName() != vdom.name) item = null;
			
			/* Create component if does not exists */
			if (item == null)
			{
				item = rtl::newInstance(vdom.name);
				item.controller = this;
				item.driver = this.driver;
				item.is_new = true;
			}
			else
			{
				item.is_new = false;
			}
			
			/* Set instance */
			item.setVirtualDom(vdom);
			
			/* Save component */
			this.components.setValue(path_str, item);
			
			/* Render component */
			item.renderComponent();
			
			/* On create */
			if (item.is_new) item.onCreate();
			
			/* On update */
			item.onUpdate();
		}
		
		/* Render function */
		else if (vdom.kind == VirtualDom::KIND_FUNCTION)
		{
			fn f = vdom.content;
			if (f != null and rtl.isFn(f)) f(vdom);
		}
		
		/* If element */
		else if (vdom.kind == VirtualDom::KIND_ELEMENT)
		{
			/* Create instance */
			if (vdom.instance == null)
			{
				if (vdom.name == "svg")
				{
					vdom.instance = document.@createElementNS("http://www.w3.org/2000/svg", "svg");
					vdom.instance._vdom = vdom;
					
					/* Patch content */
					var e = document.@createElement("div");
					e.innerHTML = "<svg>" ~ vdom.content ~ "</svg>";
					var item = e.childNodes[0];
					while (item.childNodes.length > 0)
					{
						var e = item.childNodes[0];
						vdom.instance.appendChild(e);
					}
				}
				else
				{
					vdom.instance = document.@createElement(vdom.name);
					vdom.instance._vdom = vdom;
				}
			}
		}
		
		/* If text */
		else if (vdom.kind == VirtualDom::KIND_TEXT)
		{
			/* Create instance */
			if (vdom.instance == null)
			{
				vdom.instance = document.@createTextNode( rtl::exists(vdom.content) ? vdom.content : "" );
				vdom.instance._vdom = vdom;
			}
			else
			{
				/* Set new text */
				if (vdom.instance.nodeValue != vdom.content)
				{
					vdom.instance.nodeValue = vdom.content;
				}
			}
		}
		
		/* If raw */
		else if (vdom.kind == VirtualDom::KIND_RAW)
		{
			vdom.instance = static::rawHtml(vdom.content);
		}
		
		/* Set attributes */
		if (vdom.instance != null and vdom.params != null)
		{
			var node = vdom.instance;
			Collection keys = vdom.params.keys();
			for (int i=0; i<keys.count(); i++)
			{
				string key = keys[i];
				string value = vdom.params[key];
				
				/* Check if event */
				bool is_event = false;
				string event_class_name = "";
				if (rs::substr(key, 0, 7) == "@event:")
				{
					is_event = true;
					event_class_name = rs::substr(key, 7);
				}
				if (rs::substr(0, 12) == "@eventAsync:")
				{
					is_event = true;
					event_class_name = rs::substr(key, 12);
				}
				if (is_event)
				{
					string component_name = value[0] |> default string "";
					string method_name = value[1] |> default string "";
					
					/* Add event listener */
					VirtualDom component = vdom.findParentComponent(component_name);
					if (component)
					{
						this.driver.addEventListener(event_class_name, vdom, null, component, method_name);
					}
					continue;
				}
				
				/* Bind change event */
				/*
				if (key == "@bind" or key == "@name")
				{
					string component_name = value[0] |> default string "";
					string value_name = value[1];
					string method_name = "onChangeModelEvent";
					
					VirtualDom component = vdom.findParentComponent(component_name);
					if (component)
					{
						this.driver.addEventListener
						(
							classof ChangeEvent, vdom, null, component, method_name,
							{
								"component_path_id": component.getPathId(),
								"value_name": value_name,
							}
						);
					}
				}
				*/
				
				/* Set reference */
				if (key == "@ref" or key == "@name")
				{
					string component_name = value[0] |> default string "";
					string value_name = value[1] |> default string "";
					
					/* Find parent component */
					VirtualDom component = vdom.findParentComponent(component_name);
					if (component)
					{
						component.instance[value_name] = vdom;
					}
				}
				
				/* Set value */
				if (key == "value" or key == "@bind" or key == "@name")
				{
					if (key == "@bind" or key == "@name")
					{
						Collection model_path = this.findModelPath(vdom, vdom.params[key]);
						value = this.driver.model(model_path);
					}
					
					/* Set value element */
					if (vdom.kind == VirtualDom::KIND_ELEMENT)
					{
						if (node.tagName == "INPUT" or node.tagName == "SELECT" or node.tagName == "TEXTAREA")
						{
							if (value == null and node.value != "" and value !== 0 and value !== "0")
							{
								node.value = "";
							}
							else if (node.value != value)
							{
								node.value = value;
							}
							node._old_value = value;
							continue;
						}
					}
					
					/* Set value component */
					else if (vdom.kind == VirtualDom::KIND_COMPONENT)
					{
					}
				}
				
				/*
				if (this.kind == "component" and key == "@watch")
				{
					node.watchModel(this.path_id, value);
					continue;
				}
				*/
				
				if (key[0] == "@") continue;
				if (key == "style" and value instanceof Dict)
				{
					value = rs::join(";", value.transition(string (string v, string k) => k ~ ": " ~ v));
				}
				if (vdom.kind == VirtualDom::KIND_ELEMENT)
				{
					if (node.@getAttribute(key) != value)
					{
						node.@setAttribute(key, value);
					}
				}
				
			}
		}
	}
	
	
	
	/**
	 * Update childs
	 */
	public void updateVirtualDomChilds(VirtualDom vdom)
	{
		if (vdom == null) return;
		if (vdom.kind != VirtualDom::KIND_ROOT and vdom.kind != VirtualDom::KIND_ELEMENT) return;
		
		/* Get childs */
		Collection new_childs = vdom.getChildElements();
		new_childs = new_childs
			.map( var (VirtualDom item) => item.instance )
			.filter( lib::equalNot(null) )
		;
		
		/* Patch childs */
		static::patchElemChilds(vdom.instance, new_childs);
	}
	
	
	
	/**
	 * Animation frame function
	 */
	public void animationFrame()
	{
		int items_count = this.render_vdom.count();
		
		/* Change items */
		for (int i=0; i<items_count; i++)
		{
			VirtualDom vdom = this.render_vdom[i];
			if (vdom.is_change_item)
			{
				this.renderVirtualDom(vdom);
				vdom.is_change_item = false;
			}
		}
		
		/* Update childs */
		for (int i=items_count - 1; i>=0; i--)
		{
			VirtualDom vdom = this.render_vdom[i];
			if (vdom.is_change_childs)
			{
				this.updateVirtualDomChilds(vdom);
				vdom.is_change_childs = false;
			}
		}
		
		/* Clear */
		this.render_vdom.clear();
	}
	
	
	
	/**
	 * Find model path
	 */
	Collection findModelPath(VirtualDom vdom, Collection arr)
	{
		string component_name = arr[0];
		Collection model_path = arr[1];
		if (rtl::isString(model_path)) model_path = [ model_path ];
		
		VirtualDom component = vdom.findParentComponent(component_name);
		if (component == null)
		{
			return model_path;
		}
		
		return component.instance.model_path.concat(model_path);
	}
	
	
	
	/**
	 * Find element
	 */
	static int findElementPos(var parent_elem, string path_name, string kind, string elem_name = "")
	{
		for (var i = 0; i < parent_elem.childNodes.length; i++)
		{
			var e = parent_elem.childNodes[i];
			if (e._path_name == path_name and kind == "element" and e.tagName == rs::strtoupper(elem_name))
			{
				return i;
			}
			if (e._path_name == path_name and kind == "text" and e instanceof window.Text)
			{
				return i;
			}
		}
		return -1;
	}
	
	
	
	/**
	 * Create raw html
	 */
	static Collection rawHtml(string content)
	{
		var res = new Vector();
		var e = document.@createElement('div');
		e.innerHTML = rs::trim(content)
		for (var i = 0; i < e.childNodes.length; i++) res.pushValue( e.childNodes[i] );
		return res.toCollection();
	}
	
	
	
	/**
	 * Patch DOM with new childs
	 */
	static void patchElemChilds(var parent_elem, var new_childs)
	{
		#ifcode JAVASCRIPT then
		if (new_childs == null) new_childs = [];
		
		var findElementPos = function (elem, e)
		{
			var childs = elem.childNodes;
			for (var i = 0; i < elem.childNodes.length; i++)
			{
				if (childs[i] == e)
				{
					return i;
				}
			}
			return -1;
		}
		
		var insertFirst = function (elem, e)
		{
			if (elem.childNodes.length == 0)
			{
				elem.appendChild(e);
			}
			else
			{
				elem.insertBefore(e, elem.firstChild);
			}
		}
		
		var insertAfter = function (elem, prev, e)
		{
			if (prev == null)
			{
				insertFirst(elem, e);
				return;
			}
			var next = prev.nextSibling;
			if (next == null)
			{
				elem.appendChild(e);
			}
			else
			{
				elem.insertBefore(e, next);
			}
		}
		
		
		/* Remove elems */
		var i = parent_elem.childNodes.length - 1;
		while (i >= 0)
		{
			var e = parent_elem.childNodes[i];
			if (new_childs.indexOf(e) == -1)
			{
				parent_elem.removeChild(e);
				if (e._path_id != undefined)
				{
					this.remove_keys.pushValue(null, e._path_id);
				}
				/* console.log('Remove child ', i); */
			}
			i--;
		}
		
		
		var prevElem = null;
		for (var i=0; i<new_childs.length; i++)
		{
			var new_e = new_childs[i];
			if (typeof new_e == "string")
			{
				new_e = document.createTextNode(new_e);
			}
			
			var pos = findElementPos(parent_elem, new_e);
			var flag = false;
			
			/* If new element */
			if (pos == -1)
			{
				if (prevElem == null)
				{
					insertFirst(parent_elem, new_e);
					flag = true;
					/* console.log('Insert first ', i); */
				}
				else
				{
					insertAfter(parent_elem, prevElem, new_e);
					flag = true;
					/* console.log('Insert after[1] ', i); */
				}
			}
			
			/* If existing element */
			else
			{
				if (pos - 1 < 0)
				{
					if (i != 0)
					{
						insertAfter(parent_elem, prevElem, new_e);
						flag = true;
						/* console.log('Insert after[2] ', i); */
					}
				}
				else
				{
					var prevSibling = parent_elem.childNodes[pos - 1];
					if (prevElem != prevSibling)
					{
						insertAfter(parent_elem, prevElem, new_e);
						flag = true;
						/* console.log('Insert after[3] ', i); */
					}
				}
			}
			/*
			if (flag)
			{
				var index = this.remove_keys.indexOf(null, new_e._path_id);
				if (index != -1)
					this.remove_keys.remove(null, index, 1);
			}
			*/
			prevElem = new_e;
		}
		#endif
	}
}