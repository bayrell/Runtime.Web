/*!
 *  BayLang Technology
 *
 *  (c) Copyright 2016-2024 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Runtime.Web;

use Runtime.re;
use Runtime.BaseStruct;
use Runtime.Callback;
use Runtime.Web.BasePageModel;
use Runtime.Web.BaseRoute;
use Runtime.Web.RenderContainer;


struct RouteInfo extends BaseStruct
{
	string name = null;
	string uri = null;
	string uri_match = null;
	string domain = null;
	string label = null;
	string action = null;
	string component = null;
	string model = null;
	string route_class = null;
	Dict data = null;
	Collection<Callback> middleware = [];
	Collection<string> params = [];
	Dict<string> matches = {};
	bool is_backend = false;
	bool enable_locale = true;
	int pos = 100;
	
	
	/**
	 * Init struct data
	 */
	protected void _changes(Map changes)
	{
		if (changes.has("uri") and not changes.has("uri_match"))
		{
			string uri = changes["uri"];
			string uri_match = changes["uri_match"];
			
			uri_match = re::replace("\\/", "\\/", uri);
			
			Collection matches = re::matchAll("{(.*?)}", uri);
			if (matches)
			{
				Vector<string> params = new Vector();
				
				for (int i=0; i<matches.count(); i++)
				{
					Collection arr = matches[i];
					string name = arr[1];
					uri_match = re::replace
					(
						"{" ~ name ~ "}", 
						"([^\\/]*?)", 
						uri_match
					);
					params.push(name);
				}
				
				/* Setup params */
				changes.set("params", params.toCollection());
			}
			else
			{
				changes.set("params", []);
			}
			
			changes.set("uri_match", "^" ~ uri_match ~ "$");
		}
	}
	
	
	/**
	 * Add matches
	 */
	RouteInfo addMatches(Collection matches)
	{
		Map new_matches = {};
		for (int i=0; i<this.params.count(); i++)
		{
			string param_name = this.params.get(i);
			string match_value = matches.get(i);
			new_matches.set(param_name, match_value);
		}
		return this.clone{
			"matches": new_matches,
		};
	}
	
	
	/**
	 * Resolve route
	 */
	async void resolve(RenderContainer container)
	{
		/* If route action is callable */
		if (this.action != null and rtl::isCallable(this.action))
		{
			fn f = this.action;
			await f(container);
		}
		
		/* If response is exists */
		if (container.response)
		{
			return;
		}
		
		/* Setup page model */
		string page_model_name = this.model;
		if (page_model_name != "" and rtl::class_exists(page_model_name))
		{
			await container.renderPageModel(page_model_name);
			return;
		}
		
		/* Setup component */
		if (this.component != "")
		{
			container.renderPage(this.component);
			if (this.data)
			{
				string title = this.data.get("title");
				container.layout.setPageTitle(title);
			}
			return;
		}
		
		/* Call BaseRoute */
		string class_name = classof BaseRoute;
		string method_name = "actionIndex";
		
		if (
			this.action != null and
			this.action instanceof Collection and
			this.action.count() == 2
		)
		{
			class_name = this.action.get(0);
			method_name = this.action.get(1);
		}
		
		/* Render route */
		await container.renderRoute(class_name, method_name);
	}
}