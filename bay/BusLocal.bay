/*!
 *  BayLang Technology
 *
 *  (c) Copyright 2016-2025 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Runtime.Web;

use Runtime.BaseObject;
use Runtime.BaseProvider;
use Runtime.Method;
use Runtime.Exceptions.ApiError;
use Runtime.Exceptions.ItemNotFound;
use Runtime.Exceptions.RuntimeException;
use Runtime.Web.ApiResult;
use Runtime.Web.ApiRequest;
use Runtime.Web.BaseApi;
use Runtime.Web.Bus;
use Runtime.Web.BusInterface;
use Runtime.Web.Annotations.Api;
use Runtime.Web.Annotations.ApiMethod;
use Runtime.Web.Hooks.AppHook;


class BusLocal extends BaseProvider implements BusInterface
{
	Map api_list = {};
	
	
	/**
	 * Init providers
	 */
	async void init()
	{
		await parent::init();
		
		this.api_list = {};
		
		Collection<Api> api_list = @.getEntities(classof Api);
		for (int i=0; i<api_list.count(); i++)
		{
			Api api = api_list.get(i);
			string class_name = api.name;
			Method getApiName = new Method(class_name, "getApiName");
			
			/* Save api */
			string api_name = getApiName.apply();
			this.api_list.set(api_name, api);
		}
	}
	
	
	/**
	 * Send api to frontend
	 */
	async ApiResult send(Dict params)
	{
		string api_name = params.get("api_name");
		string method_name = params.get("method_name");
		
		ApiResult result = null;
		
		try
		{
			/* Check params */
			if (api_name == "" or method_name == "")
			{
				throw new ItemNotFound("api name");
			}
			
			/* Find api */
			Api api = this.api_list.get(api_name);
			if (not api)
			{
				throw new ItemNotFound(api_name ~ "::" ~ method_name, "Api");
			}
			
			/* Find method */
			string api_method_name = "";
			ApiMethod api_method = null;
			Method getMethodsList = new Method(api.name, "getMethodsList");
			Method getMethodInfoByName = new Method(api.name, "getMethodInfoByName");
			Vector methods = getMethodsList.apply();
			for (int i=0; i<methods.count(); i++)
			{
				string name = methods.get(i);
				Vector annotations = getMethodInfoByName.apply([name]);
				ApiMethod api_method_item = annotations.find(
					bool (BaseObject obj) => obj instanceof ApiMethod
				);
				if (api_method_item and (
					api_method_item.name == method_name or
					api_method_item.name == "" and method_name == name
				))
				{
					api_method = api_method_item;
					api_method_name = name;
					break;
				}
			}
			if (api_method == null or api_method_name == "")
			{
				throw new ItemNotFound(api_name ~ "::" ~ method_name, "Api");
			}
			
			/* Call method */
			Method item = new Method(api.name, api_method_name);
			ApiRequest request = new ApiRequest{
				"data": params.get("data"),
				"storage": params.get("storage"),
			};
			result = await item.apply([request]);
		}
		catch (RuntimeException e)
		{
			result = new ApiResult();
			if (e instanceof ApiError)
			{
				if (e.result instanceof ApiResult) result = e.result;
				else result.fail(e.prev);
			}
			else result.exception(e);
		}
		
		/* If result does no exists */
		if (not result)
		{
			result = new ApiResult();
			result.exception(new ItemNotFound("ApiResult"));
		}
		
		/* Return result */
		return result;
	}
}