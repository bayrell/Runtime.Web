/*!
 *  BayLang Technology
 *
 *  (c) Copyright 2016-2025 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Runtime.Web;

use Runtime.BaseObject;
use Runtime.BaseProvider;
use Runtime.BusInterface;
use Runtime.Method;
use Runtime.Exceptions.ApiError;
use Runtime.Exceptions.ItemNotFound;
use Runtime.Exceptions.RuntimeException;
use Runtime.Web.ApiResult;
use Runtime.Web.ApiRequest;
use Runtime.Web.BaseApi;
use Runtime.Web.Bus;
use Runtime.Web.Middleware;
use Runtime.Web.Annotations.Api;
use Runtime.Web.Annotations.ApiMethod;
use Runtime.Web.Hooks.AppHook;


class BusLocal extends BaseProvider implements BusInterface
{
	Map api_list = {};
	
	
	/**
	 * Init providers
	 */
	async void init()
	{
		await parent::init();
		
		this.api_list = {};
		
		Collection<Api> api_list = @.getEntities(classof Api);
		for (int i=0; i<api_list.count(); i++)
		{
			Api api = api_list.get(i);
			string class_name = api.name;
			Method getApiName = new Method(class_name, "getApiName");
			
			/* Save api */
			string api_name = getApiName.apply();
			if (not this.api_list.has(api_name)) this.api_list.set(api_name, []);
			Vector items = this.api_list.get(api_name);
			items.push(api);
		}
	}
	
	
	/**
	 * Find api
	 */
	ApiMethod findApi(string api_name, string method_name)
	{
		Map result = {};
		
		/* Check params */
		if (api_name == "" or method_name == "")
		{
			throw new ApiError(new ItemNotFound(api_name ~ "::" ~ method_name, "Api"));
		}
		
		/* Find api */
		Vector<Api> items = this.api_list.get(api_name);
		if (not items)
		{
			throw new ApiError(new ItemNotFound(api_name ~ "::" ~ method_name, "Api"));
		}
		
		/* Find method */
		ApiMethod api_method = null;
		items.each(void (Api api) use (api_method, method_name){
			
			if (api_method) return;
			
			Method getMethodsList = new Method(api.name, "getMethodsList");
			Method getMethodInfoByName = new Method(api.name, "getMethodInfoByName");
			Vector methods = getMethodsList.apply();
			
			for (int i=0; i<methods.count(); i++)
			{
				string name = methods.get(i);
				Vector annotations = getMethodInfoByName.apply([name]);
				ApiMethod api_method_item = annotations.find(
					bool (BaseObject obj) => obj instanceof ApiMethod
				);
				if (api_method_item and api_method_item.name == method_name)
				{
					BaseApi api_item = rtl::newInstance(api.name);
					api_method = api_method_item;
					api_method.item = new Method(api_item, name);
					return;
				}
			}
		});
		
		return api_method;
	}
	
	
	/**
	 * Run middleware
	 */
	async void runMiddleware(ApiMethod api_method)
	{
		BaseApi api = api_method.item.obj;
		Vector items = api.getMiddleware();
		for (int i=0; i<items.count(); i++)
		{
			Middleware item = items.get(i);
			await item.api(api);
		}
		
		/* Run hook */
		await @.hook(AppHook::API_MIDDLEWARE, {
			"api": api
		});
	}
	
	
	/**
	 * Send api to frontend
	 */
	async ApiResult send(Dict params)
	{
		string api_name = params.get("api_name");
		string method_name = params.get("method_name");
		
		ApiResult result = null;
		
		try
		{
			/* Find api */
			ApiMethod api_method = this.findApi(api_name, method_name);
			if (api_method == null or api_method.item == null)
			{
				throw new ApiError(new ItemNotFound(api_name ~ "::" ~ method_name, "Api"));
			}
			
			/* Call method */
			ApiRequest request = new ApiRequest{
				"data": params.get("data"),
				"storage": params.get("storage"),
			};
			api_method.item.obj.setRequest(request);
			
			/* Run middleware */
			await this.runMiddleware(api_method);
			
			/* Apply */
			await api_method.apply(request);
			result = api_method.item.obj.result;
		}
		catch (RuntimeException e)
		{
			result = new ApiResult();
			if (e instanceof ApiError)
			{
				if (e.result instanceof ApiResult) result = e.result;
				else result.fail(e.prev);
			}
			else result.exception(e);
		}
		
		/* If result does no exists */
		if (not result)
		{
			result = new ApiResult();
			result.exception(new ItemNotFound("ApiResult"));
		}
		
		/* Set api name */
		result.api_name = api_name;
		result.method_name = method_name;
		
		/* Return result */
		return result;
	}
}