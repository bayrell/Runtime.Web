/*!
 *  Bayrell Runtime Library
 *
 *  (c) Copyright 2016-2024 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Runtime.Web;

use Runtime.BaseObject;
use Runtime.Callback;
use Runtime.Serializer;
use Runtime.Web.ApiResult;
use Runtime.Web.BaseModel;
use Runtime.Web.BasePageModel;
use Runtime.Web.BaseRoute;
use Runtime.Web.Cookie;
use Runtime.Web.Layout;
use Runtime.Web.RenderResponse;
use Runtime.Web.Request;
use Runtime.Web.Response;
use Runtime.Web.RouteInfo;
use Runtime.Web.WidgetSerializer;
use Runtime.Web.Hooks.AppHook;


class RenderContainer extends BaseObject
{
	public BaseRoute base_route = null;
	public Request request = null;
	public Response response = null;
	public RouteInfo route = null;
	public Layout layout = null;
	public Vector teleports = [];
	public Dict<Cookie> cookies = {};
	
	
	/**
	 * Call api
	 */
	ApiResult callApi(Map params)
	{
		return null;
	}
	
	
	/**
	 * Returns frontend environments
	 */
	Map getFrontendEnvironments()
	{
		Map environments = {};
		
		/* Setup environments */
		Collection arr = ["CLOUD_ENV", "DEBUG", "LOCALE", "TZ", "TZ_OFFSET", "ROUTE_PREFIX"];
		
		/* Call hook */
		Dict params = @.callHook(AppHook::ENVIRONMENTS, {
			"arr": arr,
			"environments": environments,
		});
		
		/* Get result */
		arr = params.get("arr");
		environments = params.get("environments");
		
		/* Copy environments */
		for (int i=0; i<arr.count(); i++)
		{
			string name = arr.get(i);
			environments.set(name, @.env(name));
		}
		
		return environments;
	}
	
	
	/**
	 * Export data
	 */
	Map exportData()
	{
		Map data = {
			"entry_point": @.entry_point,
			"modules": @.start_modules,
			"environments": this.getFrontendEnvironments(),
		};
		
		/* Create serializer */
		Serializer serializer = new WidgetSerializer();
		serializer.setCallback("processFrontendData");
		serializer.setFlag(Serializer::ALLOW_CLASS_NAME);
		serializer.setFlag(Serializer::EXPORT);
		
		/* Export data */
		serializer.process(this, "layout", data);
		
		/* Call hook */
		@.callHook(AppHook::EXPORT_CONTAINER_DATA, {
			"container": this,
			"data": data,
		});
		
		return data;
	}
	
	
	/**
	 * Import data
	 */
	void importData(Map data)
	{
		/* Call hook */
		@.callHook(AppHook::IMPORT_CONTAINER_DATA_BEFORE, {
			"container": this,
			"data": data,
		});
		
		/* Create serializer */
		Serializer serializer = new WidgetSerializer();
		serializer.setCallback("processFrontendData");
		serializer.setFlag(Serializer::ALLOW_CLASS_NAME);
		serializer.setFlag(Serializer::IMPORT);
		
		/* Load data */
		this.layout.processFrontendData(serializer, data.get("layout"));
		
		/* Call hook */
		@.callHook(AppHook::IMPORT_CONTAINER_DATA_AFTER, {
			"container": this,
			"data": data,
		});
	}
	
	
	/**
	 * Set response
	 */
	void setResponse(Response response)
	{
		this.response = response;
	}
	
	
	/**
	 * Render page and setup response
	 */
	void renderPage(string page_class_name = "")
	{
		this.response = new RenderResponse();
		this.layout.current_page_class = page_class_name;
		if (page_class_name != "")
		{
			this.layout.addComponent(page_class_name);
		}
	}
	
	
	/**
	 * Cancel route
	 */
	void cancelRoute()
	{
		if (this.base_route) this.base_route.cancelRoute();
	}
	
	
	/**
	 * Add cookie
	 */
	Response addCookie(Cookie cookie)
	{
		this.cookies.set(cookie.name, cookie);
	}
	
	
	/**
	 * Call route
	 */
	async void callRoute(string class_name, string method_name)
	{
		/* Cancel previous route */
		if (this.base_route != null)
		{
			this.base_route.cancelRoute();
			this.base_route = null;
		}
		
		/* Create route */
		BaseRoute base_route = rtl::newInstance(class_name, [this]);
		this.base_route = base_route;
		this.base_route.post_data = this.request.payload;
		
		/* Action before */
		await base_route.onActionBefore();
		
		/* Call route */
		if (method_name != "")
		{
			Callback route = new Callback(base_route, method_name);
			await rtl::apply(route);
		}
		
		/* Action after */
		if (not base_route.isCancelRoute())
		{
			await base_route.onActionAfter();
		}
	}
	
	
	/**
	 * Call base route
	 */
	async void callBaseRoute()
	{
		string class_name = classof BaseRoute;
		string method_name = "actionIndex";
		await this.callRoute(class_name, method_name);
	}
	
	
	/**
	 * Call page model
	 */
	async void callPageModel(string model_name)
	{
		this.layout.setPageModel(model_name);
		BasePageModel page_model = this.layout.getPageModel();
		await page_model.actionIndex(this);
	}
}