/*!
 *  Bayrell Runtime Library
 *
 *  (c) Copyright 2016-2020 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Runtime.Web;

use Runtime.BaseStruct;
use Runtime.Core.Message;
use Runtime.Core.RemoteCallAnswer;
use Runtime.Core.RemoteCallRequest;
use Runtime.Web.Cookie;
use Runtime.Web.LayoutModel;
use Runtime.Web.RenderDriver;
use Runtime.Web.Request;
use Runtime.Web.Response;
use Runtime.Web.Route;


struct RenderContainer extends BaseStruct
{
	Request request = null;
	Response response = null;
	Route route = null;
	Dict<string> route_params = null;
	LayoutModel layout = null;
	
	/* Frontend settings */
	Dict<primitive> frontend_storage = new Dict();
	Dict<primitive> frontend_env = new Dict();
	string pattern_name = "default";
	string pattern_class = "";

	/* Main RenderController */
	string frontend_controller_name = "";
	
	/* Backend */
	Dict<primitive> api_storage = new Dict();
	Dict<Cookie> new_cookies = {};
	Collection<string> new_headers = [];
	int new_http_code = 200;
	
	/* Functions */
	bool isRequestExists() => this.request != null;
	bool isResponseExists() => this.response != null;
	bool isRouteExists() => 
		this.route != null and this.route_params != null and
		this.route.class_name != "" and this.route.class_method_name != ""
	;
	bool isPageExists() => this.layout != null and this.layout.page_class != "";
	
	
	
	/**
	 * Remote bus call
	 * @param Dict items
	 * @return RemoteCallAnswer
	 */
	async RemoteCallAnswer externalBusCall(Dict items)
	{
		/* Set storage */
		items = items.copy
		{
			"storage": this.api_storage,
		};
		
		/* Send request */
		return await RenderDriver::externalBusCall(items);
	}
}