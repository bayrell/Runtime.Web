/*!
 *  Bayrell Runtime Library
 *
 *  (c) Copyright 2016-2023 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Runtime.Web.Crud;

use Runtime.lib;
use Runtime.BaseStruct;
use Runtime.Web.ApiResult;
use Runtime.Web.BaseRoute;
use Runtime.Web.Bus;
use Runtime.Web.RenderContainer;
use Runtime.Web.Crud.ItemResult;
use Runtime.Web.Crud.SearchResult;
use Runtime.Web.Crud.Rules.CrudRule;
use Runtime.ORM.Cursor;
use Runtime.ORM.Database;
use Runtime.ORM.Query;
use Runtime.ORM.Relation;


struct CrudApi extends BaseRoute
{
	
	/**
	 * Before route
	 */
	async void onActionBefore()
	{
		await parent::onActionBefore();
	}
	
	
	
	/**
	 * After route
	 */
	async void onActionAfter()
	{
		await parent::onActionAfter();
	}
	
	
	/**
	 * Returns relation name
	 */
	string getRelationName() => "";
	
	
	/**
	 * Returns model name
	 */
	string getModelName() => "";
	
	
	/**
	 * Returns rules
	 */
	Collection<CrudRule> getRules() =>
	[
	];
	
	
	/**
	 * Build search query
	 */
	Query buildSearchQuery(Query q)
	{
		string taxonomy = this.container.request.payload["taxonomy"];
		
		q = q
			|> .where("post_type", "=", taxonomy)
			|> .where("post_status", "=", "publish")
			|> .orderBy("post_modified_gmt", "desc")
		;
		
		return q;
	}
	
	
	/**
	 * Before search event
	 */
	async void onSearchBefore()
	{
		Collection<CrudRule> rules = this.getRules();
		for (int i=0; i<rules.count(); i++)
		{
			CrudRule rule = rules[i];
			await rule.onSearchBefore(this);
		}
	}
	
	
	/**
	 * After search event
	 */
	async void onSearchAfter()
	{
		Collection<CrudRule> rules = this.getRules();
		for (int i=0; i<rules.count(); i++)
		{
			CrudRule rule = rules[i];
			await rule.onSearchAfter(this);
		}
	}
	
	
	/**
	 * Action search
	 */
	async void actionSearch()
	{
		this.action = "actionSearch";
		this.api_result = new ApiResult();
		
		int page = 0;
		int pages = 0;
		
		/* Search before event */
		await this.onSearchBefore();
		
		/* Query to database */
		string relation_name = static::getRelationName();
		Query q = rtl::method(relation_name, "selectQuery")()
			|> this.buildSearchQuery
		;
		
		/* Execute query */
		Cursor c = await Database::execute(q);
		Collection items = await c.fetchAll();
		/* pages = await c.getPages();*/
		await c.close();
		
		/* Transform items */
		string model_name = static::getModelName();
		items = items
			|> lib::map(
				Dict (var item) use (model_name) => item
					|> .toModel( model_name )
			)
		;
		
		/* Set api result */
		this.api_result = this.api_result.success({
			"data": new SearchResult
			{
				"page": page,
				"pages": pages,
				"items": items,
			}
		});
		
		/* Search aftert event */
		await this.onSearchAfter();
		
		/* Set response */
		this.container.setResponse( this.api_result );
	}
	
	
	/**
	 * Action item
	 */
	async void actionItem()
	{
		this.action = "actionItem";
		this.api_result = new ApiResult();
		
		/* Search before event */
		await this.onSearchBefore();
		
		/* Query to database */
		string relation_name = static::getRelationName();
		
		Dict pk = this.container.request.payload["pk"];
		Collection filter = rtl::method(relation_name, "getPrimaryFilter")(pk);
		
		/* Build query */
		Query q = rtl::method(relation_name, "selectQuery")()
			|> .filter(filter)
			|> this.buildSearchQuery
		;
		
		/* Execute query */
		Cursor c = await Database::execute(q);
		Relation item = await c.fetch();
		await c.close();
		
		/* Transform items */
		string model_name = static::getModelName();
		item = item.toModel( model_name );
		
		/* Set api result */
		this.api_result = this.api_result.success({
			"data": new ItemResult
			{
				"data": item,
			}
		});
		
		/* Search aftert event */
		await this.onSearchAfter();
		
		/* Set response */
		this.container.setResponse( this.api_result );
	}
}
