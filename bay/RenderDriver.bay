/*!
 *  Bayrell Runtime Library
 *
 *  (c) Copyright 2016-2021 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Runtime.Web;

use Runtime.lib;
use Runtime.BaseDriver;
use Runtime.BaseStruct;
use Runtime.Entity;
use Runtime.LambdaChain;
use Runtime.LambdaChainClass;
use Runtime.Web.Events.BaseEvent;
use Runtime.Web.Events.WebEvent;
use Runtime.Web.Component;
use Runtime.Web.Listener;
use Runtime.Web.ListenerCallback;
use Runtime.Web.Message;
use Runtime.Web.RenderController;
use Runtime.Web.VirtualDom;


@LambdaChainClass{}
class RenderDriver extends BaseDriver
{
	static const string CSS_CHAIN = "Runtime.Web.RenderDriver.CSS";
	static const string CSS_VARS_CHAIN = "Runtime.Web.RenderDriver.CSS_Vars";
	
	public BaseStruct layout = null;
	Map<RenderController> controllers = new Map();
	Collection<string> default_components = new Vector();
	
	/* CSS */
	Dict css_vars = {};
	var css_elem = null;
	string new_css = "";
	string old_css = "";
	
	/* Animation */
	var animation_id = null;
	var message_id = null;
	
	/* Listeners */
	Vector<Listener> listeners = new Vector();
	Vector<Message> messages = new Vector();
	
	
	
	/**
	 * Constructor
	 */
	void constructor(string object_name = "", Entity entity = null)
	{
		parent(object_name, entity);
	}
	
	
	
	/**
	 * Start Driver
	 */
	public async void startDriver()
	{
		await parent::startDriver();
		
		/* Build css vars */
		this.css_vars = this.callChainCssVars();
		this.default_components = new Collection();
	}
	
	
	
	/**
	 * Find component
	 */
	Component findComponent(string path_id)
	{
		Collection arr = rs::split(":", path_id);
		string controller_name = arr[0];
		
		RenderController controller = this.controllers[controller_name];
		if (controller == null) return null;
		
		return controller.components[path_id];
	}
	
	
	
	/**
	 * Add event listener
	 */
	public void addEventListener(string event_class_name, VirtualDom from, VirtualDom dest,
		string callback_path_id, string callback_method_name, var event_tag = null)
	{
		string from_path = (from != null) ? from.getPathId() : "";
		string dest_path = (dest != null) ? dest.getPathId() : "";
		Listener listener = this.listeners.findItem
		(
			fn (Dict item) use (from_path, dest_path, event_class_name) =>
				item["from"] == from_path and
				item["dest"] == dest_path and
				item["event_class_name"] == event_class_name
		);
		if (listener == null)
		{
			listener = new Listener
			{
				"from": from_path,
				"dest": dest_path,
				"event_class_name": event_class_name,
			};
			this.listeners.pushValue(listener);
		}
		listener.addCallback(callback_path_id, callback_method_name, event_tag);
		
		/* If is new element */
		if (from != null and from.isElement() and from.is_new_element)
		{
			string es6_event_name = rtl::applyMethod(event_class_name, "getES6EventName")
				|> default string ""
			;
			if (es6_event_name != "")
			{
				from.instance.@addEventListener(es6_event_name, method this.js_event);
			}
		}
	}
	
	
	
	/**
	 * JS Event
	 */
	void @js_event(var e)
	{
		Message msg = null;
		@ = rtl::getContext();
		var ctx = @;
		
		msg = new Message( WebEvent::fromEvent(e) );
		msg.sender = e.currentTarget._vdom;
		
		await this.processMessage(ctx, msg);
	}
	
	
	
	/**
	 * Add OnMessage callback
	 */
	static Collection addListenerCallback(Map res, Listener listener)
	{
		for (int i=0; i<listener.callbacks.count(); i++)
		{
			ListenerCallback item = listener.callbacks[i];
			string path_id = item.path_id;
			res.setValue(item.ident(), item);
		}
	}
	
	
	
	/**
	 * Find listeners
	 */
	Collection findListenersByMessage(Message msg)
	{
		string from = (msg.sender != null) ? msg.sender.getPathId() : "";
		string dest = msg.dest;
		
		Map res = new Map();
		
		/* Add listeners */
		if (from != "")
		{
			for (int i=0; i<this.listeners.count(); i++)
			{
				Listener listener = this.listeners[i];
				string event_class_name = listener.event_class_name;
				
				/* Check parents */
				Collection parents = rtl::getParents(msg.data.getClassName());
				if (parents.indexOf(event_class_name) == -1)
					continue;
				
				/* Add listener */
				if (
					(listener.from == from and listener.dest == dest) or
					(listener.from == from and dest == "") or
					(listener.dest == dest and dest != "")
				)
				{
					static::addListenerCallback(res, listener);
				}
			}
		}
		
		/* Add destination */
		if (dest != "")
		{
			ListenerCallback item = new ListenerCallback
			{
				"path_id": dest,
				"method_name": "onMessage",
				/*
				{
					"from": from,
					"dest": dest,
					"event_class_name": msg.data.getClassName(),
				},*/
			};
			res.setValue(item.ident(), item);
		}
		
		return res.values().toCollection();
	}
	
	
	
	/**
	 * Process message
	 */
	async void processMessage(Message msg)
	{
		Collection callbacks = this.findListenersByMessage(msg);
		for (int i=0; i<callbacks.count(); i++)
		{
			ListenerCallback item = callbacks[i];
			string path_id = item.path_id;
			Component component = this.findComponent(path_id);
			if (component != null)
			{
				await rtl::methodApplyAsync(component, "onMessage", [msg, item]);
			}
		}
	}
	
	
	
	/**
	 * Process messages callback
	 */
	void processMessagesCallback()
	{
		Collection messages = this.messages.toCollection();
		this.message_id = null;
		this.messages.clear();
		
		for (int i=0; i<messages.count(); i++)
		{
			Message msg = messages[i];
			await this.processMessage(msg);
		}
	}
	
	
	
	/**
	 * Process messages
	 */
	void processMessages()
	{
		if (this.message_id == null)
		{
			this.message_id = window.@setTimeout( method this.processMessagesCallback, 1 );
		}
	}
	
	
	
	/**
	 * Send message
	 */
	async void sendEvent(BaseEvent event, VirtualDom sender, string dest_path = "")
	{
		Message msg = new Message(event);
		msg.sender = sender;
		msg.dest = dest_path;
		
		/* Process message */
		this.processMessage(msg);
	}
	
	
	
	/**
	 * Add component
	 */
	RenderDriver addDefaultComponent(string component_name)
	{
		this.default_components = this.default_components.pushIm(component_name);
		return this;
	}
	
	
	
	/**
	 * Add components
	 */
	RenderDriver addDefaultComponents(Collection<string> components)
	{
		this.default_components = this.default_components.concat(components);
		return this;
	}
	
	
	
	/**
	 * Setup components
	 */
	RenderDriver setDefaultComponents(Collection<string> components)
	{
		this.default_components = components;
		return this;
	}
	
	
	
	/**
	 * Setup
	 */
	RenderDriver setLayout(BaseStruct layout)
	{
		this.layout = layout;
		return this;
	}
	
	
	
	/**
	 * Returns model
	 */
	var model(Collection<string> model_path = null, var def_val = null) =>
		rtl::attr(this.layout, model_path, def_val)
	;
	
	
	
	/**
	 * Update model
	 */
	void updateModel(Collection<string> model_path = null, var value = null)
	{
		this.layout = rtl::setAttr(this.layout, model_path, value);
	}
	
	
	
	/**
	 * Returns controller
	 */
	RenderController getController(string controller_name)
	{
		RenderController controller = this.controllers.get(controller_name, null);
		return controller;
	}
	
	
	
	/**
	 * Setup controller
	 */
	RenderDriver setupController
	(
		string controller_name, var e,
		string class_name = "", Collection model_path = null,
		Dict params = null, var content = null
	)
	{
		if (model_path == null) model_path = [];
		if (params == null) params = {};
		
		RenderController controller = this.controllers.get(controller_name, null);
		if (controller == null)
		{
			controller = new RenderController();
			controller.driver = this;
			this.controllers.setValue(controller_name, controller);
		}
		
		controller.class_name = class_name;
		controller.model_path = model_path;
		controller.elem = e;
		controller.params = params;
		controller.content = content;
		controller.controller_name = controller_name;
		
		return this;
	}
	
	
	
	/**
	 * Setup css
	 */
	RenderDriver setupCSS(var e)
	{
		this.css_elem = e;
		return this;
	}
	
	
	
	/**
	 * Get css
	 */
	string getCSS(Collection components = null)
	{
		if (components == null) components = this.default_components.toCollection();
		
		components = components.filter(lib::equalNot("")).removeDuplicatesIm();
		
		/* Get css from components */
		string css = static::getCSSFromComponents( components, this.css_vars );
		
		/* Chain css */
		css = this.callChainCss(css);
		
		return css;
	}
	
	
	
	/**
	 * Repaint All
	 */
	RenderDriver repaint()
	{
		Collection<string> keys = this.controllers.keys();
		
		/* Render controllers */
		for (int i=0; i<keys.count(); i++)
		{
			string controller_name = keys[i];
			RenderController controller = this.controllers[controller_name];
			controller.render();
		}
		
		if (this.animation_id == null)
		{
			this.animation_id = window.@requestAnimationFrame( method this.animationFrame );
		}
		
		return this;
	}
	
	
	
	/**
	 * Repaint partial
	 */
	RenderDriver repaintPartial()
	{
		Collection<string> keys = this.controllers.keys();
		
		/* Render controllers */
		for (int i=0; i<keys.count(); i++)
		{
			string controller_name = keys[i];
			RenderController controller = this.controllers[controller_name];
			controller.renderPartial();
		}
		
		if (this.animation_id == null)
		{
			this.animation_id = window.@requestAnimationFrame( method this.animationFrame );
		}
		
		return this;
	}
	
	
	
	/**
	 * Animation frame function
	 */
	void animationFrame()
	{
		@ = rtl::@getContext();
		
		Collection<string> keys = this.controllers.keys();
		
		/* Render controllers */
		for (int i=0; i<keys.count(); i++)
		{
			string controller_name = keys[i];
			RenderController controller = this.controllers[controller_name];
			controller.animationFrame();
		}
		
		/* Render css */
		if (this.css_elem != null)
		{
			this.new_css = this.getCSS();
			if (this.new_css != this.old_css)
			{
				if (this.css_elem.textContent != this.new_css) this.css_elem.textContent = this.new_css;
			}
			this.old_css = this.new_css;
		}
		
		this.animation_id = null;
	}
	
	
	
	/**
	 * Retuns css hash
	 * @param string component class name
	 * @return string hash
	 */
	static memorize string hash(string s)
	{
		string r = "";
		string a = "1234567890abcdef";
		int sz = rs::strlen(s);
		int h = 0;
		for (int i=0; i<sz; i++)
		{
			int c = rs::ord( rs::substr(s, i, 1) );
			h = ((h << 2) + (h >> 14) + c) & 65535;
		}
		int p = 0;
		while (h != 0 or p < 4)
		{
			int c = h & 15;
			h = h >> 4;
			r ~= rs::substr(a, c, 1);
			p = p + 1;
		}
		return r;
	}
	
	
	
	/**
	 * Returns css hash
	 */
	pure memorize string getCssHash(string class_name) =>
		rtl::getParents(class_name)
			|> .filter
			(
				bool (string class_name) =>
					class_name != "Runtime.BaseObject" and
					class_name != "Runtime.Core.CoreObject" and
					class_name != "Runtime.Web.Component"
			)
			|> .map
			(
				string (string class_name) => "h-" ~ static::hash(class_name)
			)
		;
	
	
	
	
	/**
	 * Returns css from components
	 */
	pure memorize string getCSSFromComponents(Collection components, Dict css_vars)
	{
		if (components == null) return "";
		
		/* Extend component */
		components = static::getRequiredComponents(components);
		
		/* Get css */
		Collection arr = components.map
		(
			string (string component_name) use (css_vars)
			{
				if (component_name == "") return "";
				if (not rtl::method_exists(component_name, "css")) return "";
				fn f = rtl::method(component_name, "css");
				string css = f(css_vars);
				return css;
			}
		);
		
		string css = rs::join("", arr);
		return css;
	}
	
	
	
	/**
	 * Returns required modules
	 * @param string class_name
	 * @return Collection<string>
	 */
	static void _getRequiredComponents
	(
		Vector<string> res,
		Map<string> cache,
		Collection<string> components
	)
	{
		if (components == null)
			return;
		
		for (int i=0; i<components.count(); i++)
		{
			string class_name = components.item(i);
			if (cache.get(class_name, false) == false)
			{
				cache.setValue(class_name, true);
				if (rtl::method_exists(class_name, "components"))
				{
					fn f = rtl::method(class_name, "components");
					Dict<string> sub_components = f();
					if (sub_components != null)
					{
						static::_getRequiredComponents(res, cache, sub_components);
					}
				}
				res.pushValue(class_name);
			}
		}
	}
	
	
	
	/**
	 * Returns all components
	 * @param Collection<string> components
	 * @return Collection<string>
	 */
	pure Collection<string> getRequiredComponents(Collection<string> components)
	{
		components = components.filter(lib::equalNot("")).removeDuplicatesIm();
		Vector<string> res = new Vector();
		Map<string> cache = new Map();
		static::_getRequiredComponents(res, cache, components);
		res = res.removeDuplicatesIm();
		return res.toCollection();
	}
	
	
	
	/**
	 * Render chain
	 * Create layout model
	 */
	@LambdaChain
	{
		"name": static::CSS_VARS_CHAIN,
		"pos": 0,
	}
	static Dict cssVarsChain(Dict css_vars, RenderDriver render)
	{
		css_vars <= colors <= new Dict
		{
			"default":
			{
				"color": "#fff",
				"background": "#fff",
				"border": "#ccc",
				"text": "#000",
				"hover-background": "#eee",
				"hover-text": "inherit",
			},
			
			"active":
			{
				"color": "#337ab7",
				"background": "#337ab7",
				"border": "#22527b",
				"text": "#fff",
				"hover-background": "#337ab7",
				"hover-text": "#fff",
			},
			
			"primary":
			{
				"color": "#337ab7",
				"background": "#337ab7",
				"border": "#22527b",
				"text": "#fff",
				"hover-background": "#286090",
				"hover-text": "#fff",
				"active-background": "#286090",
				"active-tet": "#fff",
			},
			
			"danger":
			{
				"color": "#d14b42",
				"background": "#d14b42",
				"border": "#a02e27",
				"text": "#fff",
				"hover-background": "#e60000",
				"hover-text": "#fff",
				"active-background": "#e60000",
				"active-tet": "#fff",
			},
			
			"success":
			{
				"color": "green",
				"background": "green",
				"border": "green",
				"text": "#fff",
				"hover": "green",
				"hover-text": "#fff",
				"active": "green",
				"active-tet": "#fff",
			},
			
			"error":
			{
				"color": "#d14b42",
				"background": "#d14b42",
				"border": "#a02e27",
				"text": "#fff",
				"hover-background": "#e60000",
				"hover-text": "#fff",
				"active-background": "#e60000",
				"active-tet": "#fff",
			},
			
			"warning":
			{
				"color": "yellow",
				"background": "yellow",
				"border": "yellow",
				"text": "#fff",
				"hover": "yellow",
				"hover-text": "#fff",
				"active": "yellow",
				"active-tet": "#fff",
			},
		};
		
		css_vars <= font <= new Dict
		{
			"size": "14px",
		};
		
		return [ css_vars, render ];
	}
	
	
	
	/**
	 * CSS vars chain
	 */
	Dict callChainCssVars() =>
		@.chain(static::CSS_VARS_CHAIN, [new Dict(), this])
		|> attr 0
		|> default Dict {}
	;
	
	
	
	/**
	 * CSS vars chain
	 */
	string callChainCss(string css) =>
		@.chain(static::CSS_CHAIN, [css, this])
		|> attr 0
		|> default string ""
	;
} 
