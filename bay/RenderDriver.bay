/*!
 *  Bayrell Runtime Library
 *
 *  (c) Copyright 2016-2020 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Runtime.Web;

use Runtime.BaseDriver;
use Runtime.BaseStruct;
use Runtime.Web.Component;
use Runtime.Web.Controller;
use Runtime.Web.Element;


class RenderDriver extends BaseDriver
{
	static const string RENDER_CHAIN_CSS = "Runtime.Web.RenderDriver.CSS";
	
	public BaseStruct layout = null;
	var animation_id = null;
	Vector<Map> controllers = new Vector();
	Collection<Map> components = new Collection();
	Collection path_id = [];
	
	/* CSS */
	Dict css_vars = {};
	var css_elem = null;
	string new_css = "";
	string old_css = "";
	
	
	/**
	 * Add component
	 */
	RenderDriver addComponent(string component_name)
	{
		this.components = this.components.pushIm(component_name);
		return this;
	}
	
	
	
	/**
	 * Setup
	 */
	RenderDriver setLayout(BaseStruct layout)
	{
		this.layout = layout;
		return this;
	}
	
	
	
	/**
	 * Returns model
	 */
	var model(Collection<string> model_path = null, var def_val = null) =>
		rtl::attr(this.layout, model_path, def_val)
	;
	
	
	
	/**
	 * Update model
	 */
	void updateModel(Collection<string> model_path = null, var value = null)
	{
		this.layout = rtl::setAttr(this.layout, model_path, value);
		this.repaint();
	}
	
	
	
	/**
	 * Find controller
	 */
	Controller findControllerByElem(var e)
	{
		for (int i=0; i<this.controllers.count(); i++)
		{
			Controller c = this.controllers[i];
			if (c.parent_elem == e)
			{
				return c;
			}
		}
		return null;
	}
	
	
	
	/**
	 * Setup controller
	 */
	Controller addController
	(
		var e,
		string class_name = "", Collection model_path = [],
		Dict params = {}, var content = null
	)
	{
		Controller controller = this.findControllerByElem(e);
		if (controller == null)
		{
			controller = new Controller();
			controller.driver = this;
			controller.parent_elem = e;
			controller.addComponent(this);
			this.controllers.pushValue(controller);
		}
		
		controller.class_name = class_name;
		controller.model_path = model_path;
		controller.params = params;
		controller.content = content;
		
		return controller;
	}
	
	
	
	/**
	 * Setup controller
	 */
	RenderDriver setupController
	(
		var e,
		string class_name = "", Collection model_path = [],
		Dict params = {}, var content = null
	)
	{
		this.addController(e, class_name, model_path, params, content);
		return this;
	}
	
	
	
	/**
	 * Setup css
	 */
	RenderDriver setupCSS(var e)
	{
		this.css_elem = e;
		return this;
	}
	
	
	
	/**
	 * Get css
	 */
	string getCSS()
	{
		/* Get css from components */
		string css = static::getCSSFromComponents( this.components );
		
		/* Chain css */
		list res = @.chain(static::RENDER_CHAIN_CSS, [css, this]);
		css = res[0];
		
		return css;
	}
	
	
	
	/**
	 * Repaint
	 */
	void repaint()
	{
		if (this.animation_id == null)
		{
			this.animation_id = window.requestAnimationFrame( method this.render );
		}
	}
	
	
	
	/**
	 * Render function
	 */
	void render()
	{
		/* Render controllers */
		for (int i=0; i<this.controllers.count(); i++)
		{
			Controller controller = this.controllers[i];
			controller.render();
		}
		
		/* Render css */
		if (this.css_elem != null)
		{
			this.new_css = this.getCSS();
			if (this.new_css != this.old_css)
			{
				if (this.css_elem.textContent != this.new_css) this.css_elem.textContent = this.new_css;
			}
			this.old_css = this.new_css;
		}
		
		this.animation_id = null;
	}
	
	
	
	/**
	 * Retuns css hash
	 * @param string component class name
	 * @return string hash
	 */
	static memorize string hash(string s)
	{
		string r = "";
		string a = "1234567890abcdef";
		int sz = rs::strlen(s);
		int h = 0;
		for (int i=0; i<sz; i++)
		{
			int c = rs::ord( rs::substr(s, i, 1) );
			h = ((h << 2) + (h >> 14) + c) & 65535;
		}
		int p = 0;
		while (h != 0 or p < 4)
		{
			int c = h & 15;
			h = h >> 4;
			r ~= rs::substr(a, c, 1);
			p = p + 1;
		}
		return r;
	}
	
	
	
	/**
	 * Returns css hash
	 */
	pure memorize string getCssHash(string class_name)
	{
		Collection<string> names = rtl::getParents(class_name)
			|> .filter
			(
				bool (string class_name) =>
					class_name != "Runtime.BaseObject" and
					class_name != "Runtime.Core.CoreObject" and
					class_name != "Runtime.Web.Component"
			)
			|> .map
			(
				string (string class_name) => "h-" ~ static::hash(class_name)
			)
		;
		return rs::join(" ", names);
	}
	
	
	
	/**
	 * Returns css from components
	 */
	pure memorize string getCSSFromComponents(Collection components, Dict css_vars)
	{
		if (components == null) return "";
		
		/* Extend component */
		components = static::getRequiredComponents(components);
		
		/* Get css */
		Collection arr = components.map
		(
			string (string component_name) use (css_vars)
			{
				if (not rtl::method_exists(component_name, "css")) return "";
				fn f = rtl::method(component_name, "css");
				string css = f(css_vars);
				return css;
			}
		);
		
		string css = rs::join("", arr);
		return css;
	}
	
	
	
	/**
	 * Returns required modules
	 * @param string class_name
	 * @return Collection<string>
	 */
	static void _getRequiredComponents
	(
		Vector<string> res,
		Map<string> cache,
		Collection<string> components
	)
	{
		if (components == null)
			return;
		
		for (int i=0; i<components.count(); i++)
		{
			string class_name = components.item(i);
			if (cache.get(class_name, false) == false)
			{
				cache.setValue(class_name, true);
				if (rtl::method_exists(class_name, "components"))
				{
					fn f = rtl::method(class_name, "components");
					Dict<string> sub_components = f();
					if (sub_components != null)
					{
						static::_getRequiredComponents(res, cache, sub_components);
					}
				}
				res.pushValue(class_name);
			}
		}
	}
	
	
	
	/**
	 * Returns all components
	 * @param Collection<string> components
	 * @return Collection<string>
	 */
	pure Collection<string> getRequiredComponents(Collection<string> components)
	{
		Vector<string> res = new Vector();
		Map<string> cache = new Map();
		static::_getRequiredComponents(res, cache, components);
		res = res.removeDuplicatesIm();
		return res.toCollection();
	}
} 
