/*!
 *  BayLang Technology
 *
 *  (c) Copyright 2016-2025 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Runtime.Web;

use Runtime.BaseObject;
use Runtime.SerializeInterface;
use Runtime.Serializer.MapType;
use Runtime.Serializer.ObjectType;
use Runtime.Serializer.StringType;
use Runtime.Web.Hooks.AppHook;


class RouteList extends BaseObject implements SerializeInterface
{
	Map routes = {};
	Map route_params = {};
	
	
	/**
	 * Serialize
	 */
	static void serialize(ObjectType rules)
	{
		parent(rules);
		rules.addType("routes", new MapType(new MapType(new StringType())));
		rules.addType("route_params", new MapType(new StringType()));
	}
	
	
	/**
	 * Assign rules
	 */
	void assignRules(ObjectType rule)
	{
	}
	
	
	/**
	 * Returns url
	 */
	string url(string route_name, Dict route_params = null, Dict url_params = null)
	{
		if (not this.routes.has(route_name)) return "";
		if (route_params == null) route_params = {};
		if (url_params == null) url_params = {};
		
		/* Merge route params */
		route_params = this.route_params.concat(route_params);
		
		Map route = this.routes.get(route_name);
		Map res = @.hook(AppHook::MAKE_URL_PARAMS, {
			"route": route,
			"route_name": route_name,
			"route_params": route_params,
			"url_params": url_params,
		});
		
		string domain = route.get("domain");
		string url = route.get("uri");
		if (route_params != null and url != null)
		{
			route_params.each(
				void (string value, string key) use (url)
				{
					int pos = rs::indexOf(url, "{" ~ key ~ "}");
					if (pos >= 0)
					{
						url = rs::replace("{" ~ key ~ "}", value, url);
					}
				}
			);
		}
		
		/* Set url */
		if (url == null) url = "";
		
		/* Add domain */
		string url_with_domain = url;
		if (domain)
		{
			url_with_domain = "//" ~ domain ~ url;
		}
		
		/* Make url */
		res = @.hook(AppHook::MAKE_URL, {
			"domain": domain,
			"route": route,
			"route_name": route_name,
			"route_params": route_params,
			"url": url,
			"url_with_domain": url_with_domain,
			"url_params": url_params ? url_params : {},
		});
		
		bool is_domain = url_params ? url_params.get("domain", true) : true;
		return is_domain ? res.get("url_with_domain") : res.get("url");
	}
}