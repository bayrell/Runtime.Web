/*!
 *  Bayrell Runtime Library
 *
 *  (c) Copyright 2016-2023 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Runtime.Web;

use Runtime.re;
use Runtime.lib;
use Runtime.BaseProvider;
use Runtime.Vector;
use Runtime.Web.AppHook;
use Runtime.Web.BaseRoute;
use Runtime.Web.RenderContainer;
use Runtime.Web.Request;
use Runtime.Web.Route;
use Runtime.Web.RouteInfo;


class RouteList extends BaseProvider
{
	Dict<RouteInfo> routes_list = {};
	
	
	/**
	 * Start provider
	 */
	async void start()
	{
		Map routes_list = new Map();
		
		Collection<Route> routes = @.entities.filter( lib::isInstance(classof Route) );
		for (int i=0; i<routes.count(); i++)
		{
			Route info = routes[i];
			if (info.name)
			{
				/* Get method getRoutes */
				fn getRoutes = rtl::method(info.name, "getRoutes");
				
				/* Create routes */
				Collection<RouteInfo> routes_info = getRoutes();
				for (int j=0; j<routes_info.count(); j++)
				{
					RouteInfo route = routes_info[j];
					if (rtl::isString(route.method))
					{
						route <= method <= [info.name, route.method];
					}
					routes_list.setValue( route.name, route );
				}
			}
		}
		
		await @.callAsyncHook(AppHook::ROUTES_INIT, {
			"routes": this,
			"routes_list": routes_list,
		});
		
		this.routes_list = routes_list.toDict();
	}
	
	
	
	/**
	 * Find route
	 */
	RouteInfo findRoute(RenderContainer container)
	{
		Route route = null;
		Request request = container.request;
		Dict<string> params = null;
		
		string request_uri = request.uri;
		string route_prefix = request.route_prefix;
		
		request_uri = static::splitRoutePrefix(request_uri, route_prefix);
		if (request_uri === null) return null;
		if (request_uri == "") request_uri = "/";
		
		/* Find route */
		Collection<string> route_names = this.routes_list.keys();
		for (int i=0; i<route_names.count(); i++)
		{
			string route_name = route_names[i];
			RouteInfo route = this.routes_list[route_name];
			
			if (route.domain == request.host or route.domain == "")
			{
				Collection<string> matches = re::matchAll( route.uri_match, request_uri );
				if (matches) matches = matches.get(0, null).removeFirstIm();
				
				Dict d = @.callAsyncHook(AppHook::MATCH_ROUTE, {
					"route": route,
					"request_uri": request_uri,
					"container": container,
					"matches": matches,
				});
				route = d["route"];
				matches = d["matches"];
				
				if (matches != null)
				{
					route = route.addMatches(matches);
					return route;
				}
			}
		}
		
		return null;
	}
	
	
	/**
	 * Split route prefix
	 */
	pure string splitRoutePrefix(string request_uri, string route_prefix)
	{
		int prefix_len = rs::strlen(route_prefix);
		if (prefix_len > 0)
		{
			int pos = rs::search(request_uri, route_prefix);
			if (pos == -1)
			{
				return null;
			}
			request_uri = rs::substr(request_uri, prefix_len);
		}
		if (request_uri == "") request_uri = "/";
		return request_uri;
	}
}