/*!
 *  BayLang Technology
 *
 *  (c) Copyright 2016-2025 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Runtime.Web.Hooks;

use Runtime.Hooks.BaseHook;
use Runtime.Hooks.RuntimeHook;
use Runtime.Web.BaseLayoutModel;
use Runtime.Web.RenderContainer;
use Runtime.Web.RouteInfo;
use Runtime.Web.RouteProvider;


class AppHook extends BaseHook
{
	static const string ASSETS = "runtime.web.app::assets";
	static const string CALL_API_BEFORE = "runtime.web.app::call_api_before";
	static const string CLIENT_IP = "runtime.web.app::client_ip";
	static const string FIND_API = "runtime.web.app::find_api";
	static const string FIND_ROUTE_BEFORE = "runtime.web.app::find_route_before";
	static const string FIND_ROUTE_AFTER = "runtime.web.app::find_route_after";
	static const string MAKE_URL = "runtime.web.app::make_url";
	static const string MAKE_URL_PARAMS = "runtime.web.app::make_url_params";
	static const string MATCH_ROUTE = "runtime.web.app::match_route";
	static const string RESPONSE = "runtime.web.app::response";
	static const string ROUTES_INIT = "runtime.web.app::routes_init";
	static const string ROUTE_AFTER = "runtime.web.app::route_after";
	static const string ROUTE_MIDDLEWARE = "runtime.web.app::route_middleware";
	static const string ROUTE_BEFORE = "runtime.web.app::route_before";
	
	
	/**
	 * Register hooks
	 */
	void register_hooks()
	{
		parent();
		
		/* Async hooks */
		this.provider.setAsync([
			static::FIND_ROUTE_AFTER,
			static::FIND_ROUTE_BEFORE,
			static::ROUTES_INIT,
			static::ROUTE_AFTER,
			static::ROUTE_MIDDLEWARE,
			static::ROUTE_BEFORE,
		]);
		
		/* Hooks */
		#ifdef BACKEND then
		this.register(static::ROUTE_BEFORE, "setupClientIP");
		this.register(static::ROUTE_BEFORE, "setupRoute");
		this.register(static::ROUTE_BEFORE, "setupSiteName");
		#endif
	}
	
	
	#ifdef BACKEND then
	
	
	/**
	 * Setup client IP
	 */
	async void setupClientIP(Map params)
	{
		RenderContainer container = params.get("container");
		BaseLayoutModel layout = container.layout;
		if (not layout) return;
		
		string client_ip = container.request.getClientIP();
		layout.storage.backend.set("client_ip", client_ip);
	}
	
	
	/**
	 * Route after
	 */
	async void setupRoute(Map params)
	{
		RenderContainer container = params.get("container");
		BaseLayoutModel layout = container.layout;
		if (not layout) return;
		
		layout.storage.set("request", container.request);
		layout.storage.set("route", container.route);
	}
	
	
	/**
	 * Setup site name
	 */
	async void setupSiteName(Map params)
	{
		RenderContainer container = params.get("container");
		BaseLayoutModel layout = container.layout;
		if (not layout) return;
		
		layout.storage.set("site_name", layout.getSiteName());
	}
	
	#endif
}