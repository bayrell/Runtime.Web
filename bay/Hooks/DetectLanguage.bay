/*!
 *  BayLang Technology
 *
 *  (c) Copyright 2016-2025 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Runtime.Web.Hooks;

use Runtime.re;
use Runtime.lib;
use Runtime.BaseLayout;
use Runtime.Entity.Hook;
use Runtime.Hooks.RuntimeHook;
use Runtime.Web.RedirectResponse;
use Runtime.Web.RenderContainer;
use Runtime.Web.Request;
use Runtime.Web.RouteInfo;
use Runtime.Web.Hooks.AppHook;


class DetectLanguage extends RuntimeHook
{
	string default_language = "";
	string route_match_name = "lang";
	Vector<string> allowed_languages = [];
	
	
	/**
	 * Create hook
	 */
	static DetectLanguage hook(Dict params) => new Hook(classof DetectLanguage, params);
	
	
	/**
	 * Init params
	 */
	void initParam(Dict params)
	{
		parent(params);
		
		if (params.has("allowed_languages"))
		{
			this.allowed_languages = params.get("allowed_languages");
		}
		if (params.has("default_language"))
		{
			this.default_language = params.get("default_language");
		}
		if (params.has("route_match_name"))
		{
			this.route_match_name = params.get("route_match_name");
		}
	}


	/**
	 * Register hooks
	 */
	void register_hooks()
	{
		this.register(AppHook::ROUTE_BEFORE, "routeBefore");
		this.register(AppHook::MAKE_URL_PARAMS, "make_url_params");
	}
	
	
	#ifdef BACKEND then
	
	
	/**
	 * Route before
	 */
	async void routeBefore(Map params)
	{
		RenderContainer container = params.get("container");
		if (container.layout == null) return;
		if (container.route == null) return;
		if (container.route.matches == null) return;
		if (not container.route.matches.has(this.route_match_name)) return;
		
		/* Detect lang */
		container.layout.lang = container.route.matches.get(this.route_match_name);
	}
	
	#endif
	
	
	/**
	 * Make url params
	 */
	void make_url_params(Map params)
	{
		BaseLayout layout = params.get("layout");
		Map route_params = params.get("route_params");
		if (not route_params.has(this.route_match_name))
		{
			route_params.set(this.route_match_name, layout.lang);
		}
	}
}