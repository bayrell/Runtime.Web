/*!
 *  BayLang Technology
 *
 *  (c) Copyright 2016-2025 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Runtime.Web.Hooks;

use Runtime.Entity.Hook;
use Runtime.Hooks.RuntimeHook;


class Components extends RuntimeHook
{
	Vector items = {
		"components": [],
		"footer": [],
		"header": [],
	};
	int priority = 100;
	string strategy = "appendItems";
	
	
	/**
	 * Hook factory
	 */
	static Hook hook(Vector items, int priority = 100)
	{
		return new Hook(static::getClassName(), {"components": items, "priority": priority});
	}
	
	
	/**
	 * Prepend item
	 */
	static Hook prependItems(Vector items, int priority = 100)
	{
		return new Hook(static::getClassName(), {
			"components": items, "priority": priority, "strategy": "prependItems"
		});
	}
	
	
	/**
	 * Hook factory
	 */
	static Hook header(Vector items, int priority = 100)
	{
		return new Hook(static::getClassName(), {"header": items, "priority": priority});
	}
	
	
	/**
	 * Hook factory
	 */
	static Hook footer(Vector items, int priority = 100)
	{
		return new Hook(static::getClassName(), {"footer": items, "priority": priority});
	}
	
	
	/**
	 * Init params
	 */
	void initParams(Dict params)
	{
		parent(params);
		if (params == null) return;
		if (params.has("priority")) this.priority = params.get("priority");
		if (params.has("strategy")) this.strategy = params.get("strategy");
		if (params.has("components")) this.items.set("components", params.get("components"));
		if (params.has("footer")) this.items.set("footer", params.get("footer"));
		if (params.has("header")) this.items.set("header", params.get("header"));
	}
	
	
	/**
	 * Register hooks
	 */
	void register_hooks()
	{
		this.register(RuntimeHook::COMPONENTS, "components", this.priority);
		this.register(RuntimeHook::LAYOUT_HEADER, "render_head", this.priority);
		this.register(RuntimeHook::LAYOUT_FOOTER, "render_footer", this.priority);
	}
	
	
	/**
	 * Add action
	 */
	void action(Vector arr, Vector items)
	{
		if (this.strategy == "appendItems") arr.appendItems(items);
		else arr.prependItems(items);
	}
	
	
	/**
	 * Components
	 */
	void components(Map params)
	{
		this.action(params.get("components"), this.items.get("components"));
	}
	
	
	/**
	 * Render head
	 */
	void render_head(Map params)
	{
		this.action(params.get("components"), this.items.get("header"));
	}
	
	
	/**
	 * Render footer
	 */
	void render_footer(Map params)
	{
		this.action(params.get("components"), this.items.get("footer"));
	}
}