/*!
 *  BayLang Technology
 *
 *  (c) Copyright 2016-2025 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Runtime.Web.Hooks;

use Runtime.Entity.Hook;
use Runtime.Hooks.BaseHook;
use Runtime.Web.BaseLayoutModel;
use Runtime.Web.RenderContainer;
use Runtime.Web.Request;
use Runtime.Web.RouteModel;
use Runtime.Web.Hooks.SeoModel;
use Runtime.Web.Hooks.AppHook;


class CanonicalUrl extends BaseHook
{
	Dict query = null;
	
	
	/**
	 * Create hook
	 */
	static Hook hook(Map<Vector> query) => new Hook(
		classof CanonicalUrl,
		{
			"query": query
		}
	);
	
	
	/**
	 * Init params
	 */
	void initParams(Dict params)
	{
		parent(params);
		
		if (params == null) return;
		if (params.has("query")) this.query = params.get("query");
	}
	
	
    /**
	 * Register hooks
	 */
	void register_hooks()
	{
		this.register(AppHook::ROUTE_BEFORE, "routeBefore");
	}
    
	
	/**
	 * Route before
	 */
	void routeBefore(Map params)
	{
		RenderContainer container = params.get("container");
		BaseLayoutModel layout = container.layout;
		RouteModel route = layout.get("route");
		SeoModel seo = layout.get("seo");
		if (not seo) return;
		if (not route) return;
		if (route.uri == null) return;
		
		/* Build canonical url */
		string canonical_url = layout.url(
			route.name, route.matches, {"domain": false}
		);
		
		/* Add get parameters */
		Request request = layout.get("request");
		Collection<string> keys = rtl::list(request.query.keys()).sort();
		for (int i=0; i<keys.count(); i++)
		{
			string key = keys.get(i);
			if (not this.query.has(key)) continue;
			if (this.query.get(key).indexOf(route.name) == -1) continue;
			
			string value = request.query.get(key);
			canonical_url = rs::url_get_add(
				canonical_url,
				key,
				value
			);
		}
		
		/* Set canonical url */
		seo.setCanonicalUrl(canonical_url);
	}
}