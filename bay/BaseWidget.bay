/*!
 *  Bayrell Runtime Library
 *
 *  (c) Copyright 2016-2023 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Runtime.Web;

use Runtime.lib;
use Runtime.BaseObject;
use Runtime.SerializeInterface;
use Runtime.Web.BaseModel;
use Runtime.Web.LayoutModel;
use Runtime.Web.RenderContainer;
use Runtime.Web.RouteList;


class BaseWidget extends BaseModel
{
	LayoutModel layout = null;
	string component = "";
	string widget_name = "";
	Map widgets = {};
	Collection model_path = [];
	
	
	/**
	 * Create model
	 */
	void constructor(Dict params = null)
	{
		parent(params);
		if (params)
		{
			if (params.has("layout")) this.layout = params.get("layout");
			if (params.has("component")) this.component = params.get("component");
			if (params.has("widget_name")) this.widget_name = params.get("widget_name");
		}
	}
	
	
	/**
	 * Add widget
	 */
	BaseWidget addWidget(BaseWidget new_widget)
	{
		string widget_name = new_widget.widget_name;
		this.widgets.set(widget_name, new_widget);
		new_widget.model_path = this.model_path.concat(["widgets", widget_name]);
		if (new_widget.layout != null) new_widget.layout = this.layout;
		return new_widget;
	}
	
	
	/**
	 * Returns widget by name
	 */
	BaseWidget getWidget(string widget_name) => this.widgets.get(widget_name);
	
	
	/**
	 * Load data
	 */
	async void loadData()
	{
		Collection widgets_keys = this.widgets.keys();
		for (int i=0; i<widgets_keys.count(); i++)
		{
			string widget_key = widgets_keys.get(i);
			BaseWidget widget = this.widgets.get(widget_key);
			await widget.loadData();
		}
	}
	
	
	/**
	 * Import data
	 */
	void importData(Map import_data)
	{
		this.importWidgets(import_data.get("widgets"));
	}
	
	
	/**
	 * Import widgets
	 */
	void importWidgets(Map widgets_data)
	{
		Collection<string> widgets_keys = widgets_data.keys();
		for (int i=0; i<widgets_keys.count(); i++)
		{
			string widget_key = widgets_keys.get(i);
			
			BaseWidget widget = this.widgets.get(widget_key);
			Map widget_data = widgets_data.get(widget_key);
			
			if (widget == null)
			{
				string class_name = widget_data.get("__class_name__");
				widget = rtl::newInstance(class_name, [{
					"layout": this.layout,
				}]);
				this.addWidget(widget);
			}
			
			widget.importData(widget_data);
		}
	}
	
	
	/**
	 * Export data
	 */
	Map exportData()
	{
		Map data = parent::exportData();
		data.set("widget_name", this.widget_name);
		data.set("widgets", this.exportWidgets());
		return data;
	}
	
	
	/**
	 * Export widgets
	 */
	Map exportWidgets()
	{
		Map res = {};
		
		Collection widgets_keys = this.widgets.keys();
		for (int i=0; i<widgets_keys.count(); i++)
		{
			string widget_key = widgets_keys.get(i);
			BaseWidget widget = this.widgets.get(widget_key);
			res.set(widget_key, widget.exportData());
		}
		
		return res;
	}
	
	
	/**
	 * Returns url
	 */
	string url(string route_name, Dict route_params = null)
	{
		if (not this.layout.routes.has(route_name)) return null;
		
		string url = this.layout.routes.get(route_name);
		if (route_params == null) return url;
		
		route_params.each(
			void (string value, string key) use (url)
			{
				int pos = rs::indexOf(url, "{" ~ key ~ "}");
				if (pos >= 0)
				{
					url = rs::replace("{" ~ key ~ "}", value, url);
				}
				else
				{
					url = rs::url_get_add(url, key, value);
				}
			}
		);
		
		return url;
	}
}