/*!
 *  Bayrell Runtime Library
 *
 *  (c) Copyright 2016-2021 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Runtime.Web;

use Runtime.BaseObject;
use Runtime.BaseStruct;
use Runtime.Web.Events.BaseEvent;
use Runtime.Web.Events.ChangeEvent;
use Runtime.Web.Events.SignalEvent;
use Runtime.Web.ListenerCallback;
use Runtime.Web.Message;
use Runtime.Web.RenderController;
use Runtime.Web.RenderDriver;
use Runtime.Web.VirtualDom;


class Component extends BaseObject
{
	RenderDriver driver = null;
	RenderController controller = null;
	Collection model_path = null;
	primitive old_model = null;
	primitive new_model = null;
	VirtualDom vdom = null;
	Vector<Component> childs = new Vector();
	Dict params = null;
	Dict old_params = null;
	Dict old_watch_model = null;
	Dict new_watch_model = null;
	bool is_new = false;
	
	
	
	/**
	 * Returns path id
	 */
	string getPathId() => rs::join(":", this.vdom.path_id);
	
	
	
	/**
	 * Returns model
	 */
	var model(Collection<string> model_path = null, var def_val = null) =>
		this.model_path == null ? null :
			rtl::attr
			(
				this.driver.layout,
				this.model_path.concat
				(
					(rtl::isEmpty(model_path)) ? null :
					(model_path instanceof Collection) ? model_path : [ model_path ]
				),
				def_val
			)
	;
	
	
	
	/**
	 * Update model and repaint
	 */
	void update(string method_name = "")
	{
		var args = new Vector();
		#ifcode JAVASCRIPT then
		for (var i=((typeof ctx == "undefined") ? 1 : 2); i<arguments.length; i++) args.pushValue(ctx, arguments[i]);
		#endif
		
		/* Update model */
		this.updateModel(method_name, args);
		
		/* Repaint */
		this.repaint();
	}
	
	
	
	/**
	 * Update model and repaint current component
	 */
	void updatePartial(string method_name = "")
	{
		var args = new Vector();
		#ifcode JAVASCRIPT then
		for (var i=((typeof ctx == "undefined") ? 1 : 2); i<arguments.length; i++) args.pushValue(ctx, arguments[i]);
		#endif
		
		/* Update model */
		this.updateModel(method_name, args);
		
		/* Repaint partial */
		this.repaintPartial();
	}
	
	
	
	/**
	 * Update only model
	 */
	void updateModel(string method_name = "", Collection args = null)
	{
		if (args == null) args = [];
		
		/* Get current model */
		BaseStruct model = this.model();
		
		/* Change model by function */
		if (rtl::method_exists(model, method_name))
		{
			fn f = rtl::method(model, method_name);
			model = rtl::apply(f, args);
		}
		
		/* Static function */
		if (rtl::method_exists(model.getClassName(), method_name))
		{
			args = args.unshiftIm(model);
			fn f = rtl::method(model.getClassName(), method_name);
			model = rtl::apply(f, args);
		}
		
		/* Set new model */
		this.driver.updateModel(this.model_path, model);
	}
	
	
	
	/**
	 * Set Virtual DOM
	 */
	void setVirtualDom(VirtualDom vdom)
	{
		/* Update model path */
		this.updateModelPath(vdom);
		
		/* Update parent */
		/*this.updateParent(vdom);*/
		
		/* Update parent */
		this.updateParams(vdom);
		
		/* Set new vdom */
		vdom.instance = this;
		this.vdom = vdom;
	}
	
	
	
	/**
	 * Update model path
	 */
	protected void updateModelPath(VirtualDom vdom)
	{
		/* Model path */
		Collection model_path = [];
		
		/* Find model */
		Collection params_keys = ["@bind", "@name", "@model"];
		if (vdom.params != null)
		{
			for (int params_keys_i=0; params_keys_i<params_keys.count(); params_keys_i++)
			{
				string key = params_keys[params_keys_i];
				if (vdom.params.has(key))
				{
					model_path = this.controller.findModelPath(vdom, vdom.params[key]);
					break;
				}
			}
		}
		
		/* Set model */
		this.old_model = this.new_model;
		this.new_model = this.driver.model(model_path);
		this.model_path = model_path;
	}
	
	
	
	/**
	 * Update component params
	 */
	protected void updateParams(VirtualDom vdom)
	{
		this.old_params = this.params;
		this.params = vdom.params.setIm("@path_id", vdom.path_id);
	}
	
	
	
	/**
	 * Extend component model
	 */
	pure BaseStruct extendComponentModel(BaseStruct layout, Collection model_path, Dict params) => layout;
	
	
	
	/**
	 * Render function
	 */
	pure void render(BaseStruct layout, Collection model_path, Dict params, html content) => null;
	
	
	
	/**
	 * Extended render function
	 */
	pure void render2(BaseStruct layout, Collection model_path, Dict params, html content)
	{
		layout = static::extendComponentModel(layout, model_path, params);
		return static::render(layout, model_path, params, content);
	}
	
	
	
	/**
	 * Call render component
	 */
	void renderComponent()
	{
		this.driver.layout = static::extendComponentModel(this.driver.layout, this.model_path, this.params);
		if (this.isRender())
		{
			fn f = static::render
			(
				this.driver.layout, this.model_path,
				this.params, this.vdom.content
			);
			if (f != null) f(this.vdom);
		}
	}
	
	
	
	/**
	 * Returns true if need to render component
	 */
	bool isRender()
	{
		if (this.is_new)
		{
			return true;
		}
		/* Check component model */
		if (this.new_model != this.old_model)
		{
			return true;
		}
		/* Check component params */
		if (not this.equalParams())
		{
			return true;
		}
		/* Check watch model */
		if (this.isRepaintWatch())
		{
			return true;
		}
		return false;
	}
	
	
	
	/**
	 * Watch model
	 */
	void watchModel(Collection path_id, var watch)
	{
		string class_name = watch |> attr 0 |> default string [];
		var watch = watch[1];
		if (not (watch instanceof Collection)) watch = [ watch ];
		
		this.new_watch_model = new Map();
		for (int i=0; i<watch.count(); i++)
		{
			var item = watch[i];
			Collection model_path = this.controller.findModelPath(path_id, [class_name, item]);
			string key = rs::join(".", model_path);
			this.new_watch_model.setValue(key, this.driver.model(model_path));
		}
		this.new_watch_model = this.new_watch_model.toDict();
	}
	
	
	
	/**
	 * Returns true if need to repaint component
	 */
	bool isRepaintWatch()
	{
		if (this.old_watch_model != null)
		{
			Collection keys = this.old_watch_model.keys();
			for (int i=0; i<keys.count(); i++)
			{
				string key = keys[i];
				Collection model_path = rs::split("\\.", key);
				var old_model = this.old_watch_model[key];
				var new_model = this.driver.model(model_path);
				if (new_model != old_model)
				{
					return true;
				}
			}
		}
		return false;
	}
	
	
	
	/**
	 * Equal old and new params
	 */
	bool equalParams()
	{
		var arr = new Collection().concat(this.old_params.keys()).concat(this.params.keys()).removeDuplicatesIm();
		for (int i=0; i<arr.count(); i++)
		{
			string field_name = arr[i];
			if (not this.old_params.has(field_name)) return false;
			if (not this.params.has(field_name)) return false;
			if (this.params.get(field_name) != this.old_params.get(field_name)) return false;
		}
		return true;
	}
	
	
	
	/**
	 * Call repaint all
	 */
	void repaint()
	{
		this.driver.repaint();
	}
	
	
	
	/**
	 * Call repaint component
	 */
	void repaintPartial()
	{
		this.controller.render_partial.pushValue(this.vdom);
		this.driver.repaintPartial();
	}
	
	
	
	/**
	 * On create
	 */
	void onCreate()
	{
	}
	

	
	/**
	 * On update
	 */
	void onUpdate()
	{
	}
	
	
	
	/**
	 * On handle event
	 */
	async void onMessage(Message msg, ListenerCallback listener)
	{
		BaseEvent event = msg.data;
		
		if (
			listener.method_name != "onMessage" and
			listener.method_name != "onCreate" and
			listener.method_name != "onUpdate" and
			rs::substr(listener.method_name, 0, 2) == "on"
		)
		{
			if (rtl::method_exists(this, listener.method_name))
			{
				await rtl::applyMethodAsync(this, listener.method_name, [msg, listener]);
			}
		}
		
		if (msg.dest == this.getPathId() and listener.method_name == "onMessage" and event instanceof SignalEvent)
		{
			await this.onSignal(msg, listener);
		}
		
	}
	
	
	
	/**
	 * On signal
	 */
	async void onSignal(Message<SignalEvent> msg, ListenerCallback listener)
	{
		SignalEvent signal = msg.data;
		
		if (signal.command == "update")
		{
			this.updateModel(signal.method_name, signal.args);
			this.repaint();
		}
		
		else if (signal.command == "updatePartial")
		{
			this.updateModel(signal.method_name, signal.args);
			this.repaintPartial();
		}
		
		else if (signal.command == "updateModel")
		{
			this.updateModel(signal.method_name, signal.args);
		}
	}
	
	
	
	/**
	 * Catch signal
	 */
	async void onCatchSignal(Message<SignalEvent> msg, var listener)
	{
	}
	
	
	
	/**
	 * On send signal
	 */
	async void onSendSignal(Message<BaseEvent> msg, ListenerCallback listener)
	{
		SignalEvent signal = listener["event_tag", "signal"];
		if (signal)
		{
			signal <= event <= msg.data;
			await this.sendEvent( signal, this );
		}
	}
	
	
	
	/**
	 * Send event
	 */
	async void sendEvent(BaseEvent event, var dest = null)
	{
		string dest_path = "";
		
		/* if (dest == null) dest = this.getParent(); */
		if (
			dest != null and (
				dest instanceof Component or
				dest instanceof VirtualDom
			)
		)
		{
			dest_path = dest.getPathId();
		}
		
		await this.driver.sendEvent(event, this.vdom, dest_path);
	}
	
	
	
	/**
	 * Send signal
	 */
	async void sendSignal(var dest = null, Dict items)
	{
		if (dest != null)
		{
			await this.sendEvent( new SignalEvent(items), dest );
		}
	}
	
	
	
	/**
	 * Catch signal
	 */
	public void catchEvent(string event_class_name, Component dest, string method_name)
	{
		this.driver.addEventListener
		(
			event_class_name, null, dest.vdom,
			this.getPathId(), method_name
		);
	}
	
	
	
	/**
	 * Get parent component
	 */
	Component getParent(string class_name = "") => this.vdom.getParentComponent(class_name);
	Component getParentComponent(string class_name = "") => this.vdom.getParentComponent(class_name);
	
	
	
	/**
	 * Returns css name
	 */
	pure string getCssHash()
	{
		string class_name = static::getCurrentClassName();
		Collection names = RenderDriver::getCssHash(class_name);
		return rs::join(" ", names);
	}
} 
