<!--
 *  Bayrell Runtime Library
 *
 *  (c) Copyright 2016-2023 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
-->

<class name="Runtime.Web.Component">

<use name="Runtime.rs" />
<use name="Runtime.lib" />
<use name="Runtime.RawString" />
<use name="Runtime.Web.RouteList" />


<template></template>


<script>

props Collection<string> model_path = [];


/**
 * Returns component model
 */
var model()
{
	return this.layout.model();
}


/**
 * Returns component class name
 */
string _class_name(var names)
{
	#switch
	
	#case ifcode PHP then
	$names[] = static::getCssHash(static::getClassName());
	return implode(" ", $names);
	
	#case ifcode JAVASCRIPT then
	names.push(this.$options.getCssHash(this.$options.getClassName()));
	return names.join(" ");
	
	#endswitch
}


/**
 * Merge attrs
 */
void _merge_attrs()
{
}


/**
 * Escape html
 */
string _escape(var s)
{
	if (s instanceof RawString) return s;
	if (rtl::isString(s)) return rs::htmlEscape(s);
	return "";
}


/**
 * Render text
 */
void _t(var parent_elem, var content = null)
{
	#switch
	#case ifcode PHP then
	
	if ($content instanceof \Runtime\Collection)
	{
		$parent_elem->appendItems($content);
	}
	else
	{
		$parent_elem->push($content);
	}
	
	#case ifcode JAVASCRIPT then
	
	this._parent_elem_push(parent_elem, content);
	
	#endswitch
}


/**
 * Render element
 */
var _e(var parent_elem, string elem_name = null, var attrs = null, var content = null)
{
	var elem = null;
	
	#switch
	#case ifcode PHP then
	
	$attrs_str = "";
	
	if ($attrs != null && count($attrs) > 0)
	{
		$attrs = array_map(
			function ($value, $key)
			{
				return $key . "='" . \Runtime\rs::escapeHtml($value) . "'";
			},
			array_values($attrs),
			array_keys($attrs)
		);
		$attrs_str = " " . implode(" ", $attrs);
	}
	
	$elem = new \Runtime\Vector();
	$elem->push("<" . $elem_name . $attrs_str . ">");
	
	if ($content instanceof \Runtime\Collection)
	{
		$elem->appendItems($content);
	}
	else if (is_string($content) or $content instanceof \Runtime\RawString)
	{
		$elem->push($content);
	}
	
	$elem->push("</" . $elem_name . ">");
	$parent_elem->appendItems($elem);
	
	#case ifcode JAVASCRIPT then
	
	elem = Vue.h(elem_name, attrs, []);
	this._parent_elem_push(parent_elem, elem);
	
	#endswitch
	
	return elem;
}


/**
 * Render component
 */
var _c(var parent_elem, string component_name = null, var attrs = null, var content = null)
{
	var elem = null;
	
	#switch
	#case ifcode PHP then
	
	$component = \Runtime\rtl::newInstance($component_name);
	$component->layout = $this->layout;
	$component->model_path = $this->model_path;
	if ($attrs != null)
	{
		foreach ($attrs as $key => $value)
		{
			$component->$key = $value;
		}
	}
	$elem = $component->render();
	
	if ($elem instanceof \Runtime\Collection)
	{
		$parent_elem->appendItems($elem);
	}
	else if (is_string($elem) or $elem instanceof \Runtime\RawString)
	{
		$parent_elem->push($elem);
	}
	
	#case ifcode JAVASCRIPT then
	
	let component = use(component_name);
	if (component == undefined)
	{
		throw new Runtime.Exceptions.ItemNotFound(component_name);
	}
	
	elem = Vue.h(component, attrs, content);
	this._parent_elem_push(parent_elem, elem);
	
	#endswitch
	
	return elem;
}


/**
 * Push to parent elem
 */
void _parent_elem_push(var parent_elem, var elem)
{
	#ifcode JAVASCRIPT then
	
	if (parent_elem instanceof Array)
	{
		if (elem instanceof Array)
		{
			parent_elem += elem;
		}
		else
		{
			parent_elem.push(elem);
		}
	}
	else if (typeof parent_elem == "object")
	{
		if (parent_elem.children == null)
		{
			parent_elem.children = [];
		}
		if (elem instanceof Array)
		{
			parent_elem.children += elem;
		}
		else
		{
			parent_elem.children.push(elem);
		}
	}
	
	#endif
}


/**
 * Flatten elements
 */
void _flatten(var arr)
{
	#ifcode JAVASCRIPT then
	
	if (arr.length == 0) return null;
	if (arr.length == 1) return arr[0];
	return arr;
	
	#endif
}


/**
 * Returns components
 */
static Vector components()
{
	return [];
}


/**
 * Returns css hash
 */
pure memorize string getCssHash(string class_name) =>
	rtl::getParents(class_name)
		|> .toVector()
		|> .prepend(class_name)
		|> .filter
		(
			bool (string class_name) =>
				class_name != "Runtime.BaseObject" and
				class_name != "Runtime.Web.Component" and
				class_name != ""
		)
		|> .map
		(
			string (string class_name) => "h-" ~ static::hash(class_name)
		)
		|> lib::join(" ")
	;


/**
 * Retuns css hash
 * @param string component class name
 * @return string hash
 */
pure memorize string hash(string s)
{
	int h = rs::hash(s, true, 337, 65537) + 65537;
	string res = rs::toHex(h);
	return rs::substr(res, -4);
}


/**
 * Is component
 */
pure bool isComponent(string tag_name)
{
	string ch1 = rs::substr(tag_name, 0, 1);
	string ch2 = rs::upper(ch1);
	return tag_name != "" and (ch1 == "{" or ch1 == ch2);
}

</script>

</class>
