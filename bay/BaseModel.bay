/*!
 *  Bayrell Runtime Library
 *
 *  (c) Copyright 2016-2023 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Runtime.Web;

use Runtime.BaseStruct;
use Runtime.Callback;
use Runtime.ModelProxy;
use Runtime.Web.BaseRoute;
use Runtime.Web.RenderProvider;
use Runtime.Web.RouteList;


struct BaseModel extends BaseStruct
{
	string widget_name = "";
	string widget_path = "";
	Dict widgets = {};
	Collection<Callback> events = [];
	
	
	/**
	 * Add widget
	 */
	BaseModel addWidget(BaseModel widget) => this.clone{
		"widgets": this.widgets.setIm(widget.widget_name, widget)
	};
	
	
	/**
	 * Returns widget
	 */
	BaseModel getWidget(string widget_name) => this.widgets.get(widget_name);
	
	
	/**
	 * Set widget path
	 */
	BaseModel setWidgetPath(string widget_path) => this.clone
	{
		"widget_path": widget_path,
	};
	
	
	/**
	 * Add event
	 */
	BaseModel addEvent(Callback event) => this.clone
	{
		"events": this.events.pushIm(event)
	};
	
	
	/**
	 * Create widget
	 */
	async static void createWidget(ModelProxy<BaseModel> widget, BaseRoute route)
	{
	}
	
	
	/**
	 * Init widget
	 */
	async static void initWidget(ModelProxy<BaseModel> widget, BaseRoute route)
	{
	}
	
	
	/**
	 * Load widget
	 */
	async static void loadWidget(ModelProxy<BaseModel> widget, BaseRoute route)
	{
	}
	
	
	/**
	 * Setup widget
	 */
	async static void setupWidget(ModelProxy<BaseModel> widget, BaseRoute route)
	{
		await widget.commitAsync("initWidget", route);
		await widget.commitAsync("loadWidget", route);
		await widget.commitAsync("setupSubWidgets", route);
	}
	
	
	/**
	 * Setup sub widgets
	 */
	async static void setupSubWidgets(ModelProxy<BaseModel> widget, BaseRoute route)
	{
		Dict widgets = widget.data("widgets");
		Collection widgets_keys = widgets.keys();
		
		for (int i=0; i<widgets_keys.count(); i++)
		{
			string widget_name = widgets_keys.get(i);
			ModelProxy<BaseModel> sub_widget = widget.proxy(["widgets", widget_name]);
			await sub_widget.commitAsync("setupWidget", route);
		}
	}
	
	
	/**
	 * Widget event
	 */
	async static Dict event
	(
		ModelProxy<BaseModel> widget,
		string action, Dict obj, var tag = null
	)
	{
		return obj;
	}
	
	
	/**
	 * Call widget event
	 */
	async static Dict callEvent(ModelProxy<BaseModel> widget, string action, Dict obj)
	{
		/* Get models */
		BaseModel widget_data = widget.data();
		
		/* Call event */
		obj = await widget_data::event(widget, action, obj);
		
		/* Call events */
		for (int i=0; i<widget_data.events.count(); i++)
		{
			Callback f = widget_data.events[i];
			obj = await f.callAsync([widget, action, obj, f.tag]);
		}
		
		return obj;
	}
	
	
	/**
	 * Returns url
	 */
	static string url(string route_name, Dict route_params = null)
	{
		RouteList routes = @.provider(classof RouteList);
		return routes.url(route_name, route_params);
	}
	
}