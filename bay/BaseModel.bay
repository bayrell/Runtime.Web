/*!
 *  Bayrell Runtime Library
 *
 *  (c) Copyright 2016-2023 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Runtime.Web;

use Runtime.BaseStruct;
use Runtime.ModelProxy;
use Runtime.Web.BaseRoute;
use Runtime.Web.RenderProvider;
use Runtime.Web.RouteList;


struct BaseModel extends BaseStruct
{
	string widget_name = "";
	string widget_path = "";
	Dict widgets = {};
	
	
	/**
	 * Add widget
	 */
	BaseModel addWidget(BaseModel widget) => this.clone{
		"widgets": this.widgets.setIm(widget.widget_name, widget)
	};
	
	
	/**
	 * Returns widget
	 */
	BaseModel getWidget(string widget_name) => this.widgets.get(widget_name);
	
	
	/**
	 * Set widget path
	 */
	BaseModel setWidgetPath(string widget_path) => this.clone
	{
		"widget_path": widget_path,
	};
	
	
	/**
	 * Create widget
	 */
	async static void createWidget(ModelProxy<BaseModel> widget, BaseRoute route)
	{
	}
	
	
	
	/**
	 * Init widget
	 */
	async static void initWidgets(ModelProxy<BaseModel> model, BaseRoute route)
	{
		Dict widgets = model.data("widgets");
		Collection widgets_keys = widgets.keys();
		
		for (int i=0; i<widgets_keys.count(); i++)
		{
			string widget_name = widgets_keys.get(i);
			ModelProxy<BaseModel> widget = model.proxy(["widgets", widget_name]);
			await widget.commitAsync("initWidgets", route);
		}
	}
	
	
	/**
	 * Load widgets data
	 */
	async static void loadWidgets(ModelProxy<BaseModel> model, BaseRoute route)
	{
		Dict widgets = model.data("widgets");
		Collection widgets_keys = widgets.keys();
		
		for (int i=0; i<widgets_keys.count(); i++)
		{
			string widget_name = widgets_keys.get(i);
			ModelProxy<BaseModel> widget = model.proxy(["widgets", widget_name]);
			await widget.commitAsync("loadWidgets", route);
		}
	}
	
	
	/**
	 * Widget event
	 */
	async static Dict event
	(
		ModelProxy<BaseModel> page_model,
		ModelProxy<BaseModel> widget,
		string action, Dict obj
	)
	{
		return obj;
	}
	
	
	/**
	 * Call widget event
	 */
	async static Dict callEvent(ModelProxy<BaseModel> widget, string action, Dict obj)
	{
		RenderProvider provider = @.provider(classof RenderProvider);
		
		/* Get models */
		ModelProxy<BaseModel> page_model = provider.modelProxy();
		BaseModel widget_data = widget.data();
		BaseModel page_model_data = page_model.data();
		
		/* Call event */
		obj = await widget_data::event(page_model, widget, action, obj);
		if (page_model_data != widget_data)
		{
			obj = await page_model_data::event(page_model, widget, action, obj);
		}
		
		return obj;
	}
	
	
	/**
	 * Returns url
	 */
	static string url(string route_name, Dict route_params = null)
	{
		RouteList routes = @.provider(classof RouteList);
		return routes.url(route_name, route_params);
	}
	
}